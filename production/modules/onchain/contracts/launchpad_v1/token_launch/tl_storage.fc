#include "../../stdlib.fc";
#include "../gas.fc";

#include "tl_context.fc";

() save_data() impure inline_ref {
    cell general_state = begin_cell()
        .store_int(general_state::const::start_time, 32)
        .store_coins(general_state::fut_jet_inner_balance)
        .store_coins(general_state::fut_jet_deployed_balance)
        .store_coins(general_state::total_tons_collected)
        .end_cell();
    cell creator_round_state = begin_cell()
        .store_coins(creator_round::fut_jet_left)
        .store_coins(creator_round::fut_jet_balance)
        .store_coins(creator_round::const::fut_jet_price_reversed)
        .store_int(creator_round::const::end_time, 32)
        .end_cell();
    cell wl_round_state = begin_cell()
        .store_coins(wl_round::const::fut_jet_limit)
        .store_coins(wl_round::const::ton_limit)
        .store_coins(wl_round::ton_invested_total)
        .store_int(wl_round::const::end_time, 32)
        .end_cell();
    cell public_round_state = begin_cell()
        .store_coins(public_round::const::fut_jet_limit)
        .store_coins(public_round::fut_jet_sold)
        .store_coins(public_round::synthetic_jet_reserve)
        .store_coins(public_round::synthetic_ton_reserve)
        .store_int(public_round::const::end_time, 32)
        .end_cell();
    cell sale_state = begin_cell()
        .store_ref(general_state)
        .store_ref(creator_round_state)
        .store_ref(wl_round_state)
        .store_ref(public_round_state)
        .end_cell();
    cell sale_config = begin_cell()
        .store_coins(config::const::fut_jet_total_supply)
        .store_coins(config::const::min_ton_for_sale_success)
        .store_coins(config::const::fut_jet_dex_amount)
        .store_coins(config::const::fut_jet_platform_amount)
        .end_cell();
    cell data = begin_cell()
        .store_int(this::const::is_initialized?, 1)
        .store_coins(this::operational_needs)
        .store_slice(this::const::chief_address)
        .store_slice(this::const::creator_address)
        .store_ref(sale_config)
        .store_ref(sale_state)
        .store_ref(this::tools)
        .end_cell();
    set_data(data);
}

() load_data() impure inline_ref {
    slice ds = get_data().begin_parse();
    this::const::is_initialized? = ds~load_int(1);
    this::operational_needs = ds~load_coins();
    this::const::chief_address = ds~load_msg_addr();
    this::const::creator_address = ds~load_msg_addr();

    cell sale_config = ds~load_ref();
    slice sale_config_slice = sale_config.begin_parse();
    config::const::fut_jet_total_supply = sale_config_slice~load_coins();
    config::const::min_ton_for_sale_success = sale_config_slice~load_coins();
    config::const::fut_jet_dex_amount = sale_config_slice~load_coins();
    config::const::fut_jet_platform_amount = sale_config_slice~load_coins();
    sale_config_slice.end_parse();

    cell sale_state = ds~load_ref();
    slice sale_state_slice = sale_state.begin_parse();
    cell general_state = sale_state_slice~load_ref();
    cell creator_round_state = sale_state_slice~load_ref();
    cell wl_round_state = sale_state_slice~load_ref();
    cell pub_round_state = sale_state_slice~load_ref();
    sale_state_slice.end_parse();

    slice general_state_slice = general_state.begin_parse();
    general_state::const::start_time = general_state_slice~load_int(32);
    general_state::fut_jet_inner_balance = general_state_slice~load_coins();
    general_state::fut_jet_deployed_balance = general_state_slice~load_coins();
    general_state::total_tons_collected = general_state_slice~load_coins();
    general_state_slice.end_parse();

    slice creator_round_state_slice = creator_round_state.begin_parse();
    creator_round::fut_jet_left = creator_round_state_slice~load_coins();
    creator_round::fut_jet_balance = creator_round_state_slice~load_coins();
    creator_round::const::fut_jet_price_reversed = creator_round_state_slice~load_coins();
    creator_round::const::end_time = creator_round_state_slice~load_int(32);
    creator_round_state_slice.end_parse();

    slice wl_round_state_slice = wl_round_state.begin_parse();
    wl_round::const::fut_jet_limit = wl_round_state_slice~load_coins();
    wl_round::const::ton_limit = wl_round_state_slice~load_coins();
    wl_round::ton_invested_total = wl_round_state_slice~load_coins();
    wl_round::const::end_time = wl_round_state_slice~load_int(32);
    wl_round_state_slice.end_parse();

    slice public_round_state_slice = pub_round_state.begin_parse();
    public_round::const::fut_jet_limit = public_round_state_slice~load_coins();
    public_round::fut_jet_sold = public_round_state_slice~load_coins();
    public_round::synthetic_jet_reserve = public_round_state_slice~load_coins();
    public_round::synthetic_ton_reserve = public_round_state_slice~load_coins();
    public_round::const::end_time = public_round_state_slice~load_int(32);
    public_round_state_slice.end_parse();

    this::tools = ds~load_ref();
    ds.end_parse();
}


(slice, slice, cell, cell, cell, cell) unpack_tools(cell tools) inline_ref {
    slice tools_slice = tools.begin_parse();
    slice fut_jet_master_address = tools_slice~load_msg_addr();
    slice fut_jet_wallet_address = tools_slice~load_msg_addr();
    cell metadata = tools_slice~load_ref();
    cell fut_jet_master_code = tools_slice~load_ref();
    cell wallet_code = tools_slice~load_ref();
    cell user_vault_code = tools_slice~load_ref();
    tools_slice.end_parse();
    return (
        fut_jet_master_address, fut_jet_wallet_address,
        metadata, fut_jet_master_code, wallet_code, user_vault_code
    );
}
