#include "../../workchain.fc";
#include "../../stdlib.fc";
#include "../op-codes.fc";
#include "../utils.fc";
#include "../gas.fc";

#include "jetton_operating.fc";
#include "trades_handler.fc";
#include "tl_context.fc";
#include "tl_storage.fc";

() initialize(int query_id, slice sender_address, slice fut_jet_wallet_address, slice fut_jet_master_address) impure inline {
    (_, _, cell metadata, cell fut_jet_master_code, cell wallet_code, cell user_vault_code) = unpack_tools(this::tools);
    ;; Building new tools with initialized addresses instead of none ones
    cell new_tools = begin_cell()
        .store_slice(fut_jet_master_address)
        .store_slice(fut_jet_wallet_address)
        .store_ref(metadata)
        .store_ref(fut_jet_master_code)
        .store_ref(wallet_code)
        .store_ref(user_vault_code)
        .end_cell();
    this::tools = new_tools;
    ;; As we are not waiting for reward tokens anymore
    this::const::is_initialized? = TRUE;
}

;; Should we have something else here?
() on_bounce(int op, int query_id, int attached_value, slice sender_address, slice in_msg_body) impure inline {
    if (op == op::uv::balance_update) {
        int mode = in_msg_body~load_uint(4);
        ;; If it is not deposit
        ifnot ((mode == BALANCE_UPD_DEPOSIT_WL) | (mode == BALANCE_UPD_DEPOSIT_PUB)) {
            return ();
        }
        handle_refund_confirmation(
            query_id,
            attached_value,
            sender_address,
            in_msg_body~load_coins(),
            in_msg_body~load_coins(),
            in_msg_body~load_coins(),
            in_msg_body~load_msg_addr()
        );
    }
}


() recv_internal(int contract_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    ;; Donations are always welcome
    if (in_msg_body.slice_empty?() | in_msg_body.slice_bits() < 32) {
        return ();
    }
    (int msg_flags, slice sender_address, int attached_value, int fwd_fee_from_in_msg) = extract_full_msg_data(in_msg_full);
    (int op, int query_id) = in_msg_body~load_op_and_query_id();

    load_data();
    if (msg_flags & 1) {
        on_bounce(op, query_id, attached_value, sender_address, in_msg_body);
        return ();
    }

    if (op == op::tl::init) {
        throw_if(error::already_initialized, this::const::is_initialized?);
        raw_reserve(
            calculate_token_launch_min_storage_fee(TWO_MONTHS) + get_jetton_deployment_total_gas_cost(),
            RESERVE_REGULAR | RESERVE_BOUNCE_ON_ACTION_FAIL
        );
        initialize(query_id, sender_address, in_msg_body~load_msg_addr(), in_msg_body~load_msg_addr());
        save_data();
        return ();
    }
    if (op == op::excesses) {
        return ();
    }

    ;; Check with new position
    ;; We can't perform any operations before actual start and initialization
    throw_unless(error::wrong_time, now() > general_state::const::start_time);
    throw_unless(error::uninitialized, this::const::is_initialized?);

    ;; if (this::operational_needs > (ONE_TON / 4)) {
    ;;     cell msg = simple_transfer(this::const::chief_address, this::operational_needs * 90 / 100, "meow");
    ;;     send_raw_message(msg, SEND_MODE_REGULAR);
    ;;     this::operational_needs = 0;
    ;; }

    ;; - CN: can be sent by anyone
    ;; if there is a commission balance, returns it back to the user
    if (op == op::tl::creator_buyout) {
        throw_unless(error::unauthorized_access, equal_slices_bits(sender_address, this::const::creator_address));
        raw_reserve(attached_value, RESERVE_ALL_BUT_AMOUNT | RESERVE_BOUNCE_ON_ACTION_FAIL);
        handle_creator_buyout(attached_value);
        save_data();
        return ();
    }
    if (op == op::tl::wl_purchase) {
        raw_reserve(attached_value, RESERVE_ALL_BUT_AMOUNT | RESERVE_BOUNCE_ON_ACTION_FAIL);
        handle_wl_purchase(query_id, attached_value, sender_address);
        save_data();
        return ();
    }
    if (op == op::tl::public_purchase) {
        raw_reserve(attached_value, RESERVE_ALL_BUT_AMOUNT | RESERVE_BOUNCE_ON_ACTION_FAIL);
        handle_public_purchase(query_id, attached_value, sender_address);
        save_data();
        return ();
    }
    if (op == op::tl::refund_request) {
        ;; raw_reserve(attached_value, RESERVE_ALL_BUT_AMOUNT | RESERVE_BOUNCE_ON_ACTION_FAIL);
        handle_refund_request(query_id, attached_value, sender_address, in_msg_body~load_uint(4));
        return ();
    }
    if (op == op::tl::creator_refund) {
        throw_unless(error::unauthorized_access, equal_slices_bits(sender_address, this::const::creator_address));
        handle_creator_refund(attached_value);
        save_data();
        return ();
    }
    if (op == op::tl::refund_confirmation) {
        handle_refund_confirmation(
            query_id,
            attached_value,
            sender_address,
            in_msg_body~load_coins(),
            in_msg_body~load_coins(),
            in_msg_body~load_coins(),
            in_msg_body~load_msg_addr()
        );
        save_data();
        return ();
    }
    if (op == op::tl::deploy_jet) {
        throw_unless(error::unauthorized_access, equal_slices_bits(sender_address, this::const::chief_address));
        accept_message();
        throw_if(error::bad_request, general_state::total_tons_collected < config::const::min_ton_for_sale_success);
        throw_if(error::bad_request, general_state::fut_jet_deployed_balance > 0);
        int launch_success_treshold =
        general_state::total_tons_collected + this::operational_needs
        + get_jetton_deployment_total_gas_cost()
        + calculate_token_launch_min_storage_fee(ONE_MONTH);
        throw_arg_if(launch_success_treshold - contract_balance, error::balance_lack, contract_balance < launch_success_treshold);
        deploy_launch_jetton(query_id);
        if (contract_balance > launch_success_treshold) {
            cell excesses = excesses_msg(query_id, sender_address, contract_balance - launch_success_treshold);
            send_raw_message(excesses, SEND_MODE_IGNORE_ERRORS);
        }
        return ();
    }
    if (op == op::transfer_notification) {
        ;; Verify, that sender is our utility token wallet and no one other
        route_incoming_transfer(query_id, attached_value, sender_address, in_msg_body~load_coins(), in_msg_body~load_msg_addr(), in_msg_body);
        save_data();
        return ();
    }
    if (op == op::tl::jetton_claim_request) {
        raw_reserve(0, RESERVE_WITH_CONTRACT_BALANCE | RESERVE_BOUNCE_ON_ACTION_FAIL);
        throw_unless(error::wrong_time, now() > public_round::const::end_time);
        ;; For successful claim we need both launch jetton to send and utility jetton to reward
        throw_if(error::tokens_lack, ~ (general_state::fut_jet_deployed_balance > 0));
        handle_jetton_claim_request(query_id, attached_value, sender_address);
        return ();
    }
    if (op == op::tl::jetton_claim_confirmation) {
        raw_reserve(0, RESERVE_WITH_CONTRACT_BALANCE);
        int wl_tons = in_msg_body~load_coins();
        in_msg_body~load_coins(); ;; Skip pub_tons
        int jettons = in_msg_body~load_coins();
        slice recipient = in_msg_body~load_msg_addr();
        handle_jetton_claim_confirmation(query_id, sender_address, wl_tons, jettons, recipient);
        save_data();
        return ();
    }
    if (op == op::tl::claim_opn) {
        throw_unless(error::unauthorized_access, equal_slices_bits(sender_address, this::const::chief_address));
        throw_unless(error::balance_lack, this::operational_needs > 0);

        raw_reserve(attached_value + this::operational_needs, RESERVE_ALL_BUT_AMOUNT);
        this::operational_needs = 0;

        cell transfer_msg = simple_transfer(this::const::chief_address, 0, "opn");
        send_raw_message(transfer_msg, SEND_MODE_CARRY_ALL_BALANCE | SEND_MODE_BOUNCE_ON_ACTION_FAIL);
        save_data();
        return ();
    }
    if (op == op::upgrade) {
        throw_unless(error::unauthorized_access, equal_slices_bits(sender_address, this::const::chief_address));
        (cell new_data, cell new_code) = (in_msg_body~load_ref(), in_msg_body~load_ref());
        in_msg_body.end_parse();
        set_data(new_data);
        set_code(new_code);
        return ();
    }
    throw(error::wrong_op);
}

(int, slice, cell) get_launch_data() method_id {
    load_data();
    (_, _, cell metadata, _, _, _) = unpack_tools(this::tools);
    throw_unless(error::uninitialized, this::const::is_initialized?);
    return (config::const::fut_jet_total_supply, this::const::creator_address, build_content_cell(metadata.begin_parse()));
}

slice get_user_vault_address(slice user_address) method_id {
    load_data();
    (_, _, _, _, _, cell user_vault_code) = unpack_tools(this::tools);
    (_, slice user_vault_address) = calculate_user_vault_data(user_vault_code, user_address);
    return user_vault_address;
}

(int, int, int, int, int, int) get_money_flows() method_id {
    load_data();
    return (
        general_state::total_tons_collected,
        creator_round::fut_jet_balance,
        wl_round::ton_invested_total,
        public_round::fut_jet_sold,
        public_round::synthetic_jet_reserve,
        public_round::synthetic_ton_reserve
    );
}

(int, int, int, int, int, int, int, int) get_config() method_id {
    load_data();
    return (
        ;; Due to new creator's mechanics, we are able to creators round data only by calculating it on receiver's side
        creator_round::fut_jet_balance,
        creator_round::fut_jet_left,
        creator_round::const::fut_jet_price_reversed,

        wl_round::const::fut_jet_limit,
        public_round::const::fut_jet_limit,

        config::const::fut_jet_dex_amount,
        config::const::fut_jet_platform_amount,

        config::const::min_ton_for_sale_success
    );
}

(int, int, int, int) get_sale_timings() method_id {
    load_data();
    return (
        general_state::const::start_time,
        creator_round::const::end_time,
        wl_round::const::end_time,
        public_round::const::end_time
    );
}

;; Needed more for checks & verifications
(int, int) get_inner_data() method_id {
    load_data();
    return (
        general_state::fut_jet_deployed_balance,
        this::operational_needs
    );
}


